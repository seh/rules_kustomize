# We must accommodate loading this file from repositories generated by
# our repository rules.
visibility("public")

_TOOLS_BY_RELEASE = {
    "v4.1.1": {
        struct(os = "darwin", arch = "amd64"): "6b8dbb03abb74e9ab8e69ca3f9b6459178be11317d0ac502f922621c05fdc866",
        struct(os = "darwin", arch = "arm64"): "b8f49e105b1d2fd8c8a90ba3fc9af48db91d2d1ca3b9e788352fc7c896bbb71a",
        struct(os = "linux", arch = "amd64"): "5d4c7623283e6dfb1971957f4b755468ab64917066a8567dd50464af298f4031",
        struct(os = "linux", arch = "arm64"): "02a5fb7742469d2d132e24cb7c3f52885894043576588c6788b6813297629edd",
        struct(os = "windows", arch = "amd64"): "665161eba861d86bdb6969aa15d0d0cac3f04ce950ca12b4ee0552d7967a61fa",
        struct(os = "windows", arch = "arm64"): "bd73ebc89311de201d6d86fc20ced76d7f448579332c54093fc086a2279d905d",
    },
    "v3.19.0": {
        struct(os = "darwin", arch = "amd64"): "09a108c0abda42e45af172be65c49125354bf7cd178dbe10435e94540e49c7b9",
        struct(os = "darwin", arch = "arm64"): "31513e1193da4eb4ae042eb5f98ef9aca7890cfa136f4707c8d4f70e2115bef6",
        struct(os = "linux", arch = "amd64"): "a7f81ce08007091b86d8bd696eb4d86b8d0f2e1b9f6c714be62f82f96a594496",
        struct(os = "linux", arch = "arm64"): "440cf7add0aee27ebc93fada965523c1dc2e0ab340d4348da2215737fc0d76ad",
        struct(os = "windows", arch = "amd64"): "6488630c2e5d5945ed990fa02fd9e99f9c6792cdbcd79eb264b6cfb90179d2d1",
        struct(os = "windows", arch = "arm64"): "488f7530a1776da1b46b14e988bf305c9d7419c78e7e73aeb92f198a41c9ef6b",
    },
    "v3.17.4": {
        struct(os = "darwin", arch = "amd64"): "6dfce5d8b09442205393068f5c911b8f20958edd5b67cc26f7eb3330f93225f1",
        struct(os = "darwin", arch = "arm64"): "f4732719827a76452035641629aa92091adc4a9f1ccbc8d53a729b53e0add869",
        struct(os = "linux", arch = "amd64"): "c91e3d7293849eff3b4dc4ea7994c338bcc92f914864d38b5789bab18a1d775d",
        struct(os = "linux", arch = "arm64"): "460a31d1511abb5ad776a26a2a3f0f1382a241b2df3c6d725b0f63c9058ba15a",
        struct(os = "windows", arch = "amd64"): "8ffe44ee2aca8cf32156b42efd50e5dabda8da65ab1f10707d9666dea6fcc7e9",
        struct(os = "windows", arch = "arm64"): "ef36602d30ce0945b57cc0ceeff674128adf8918da9af6e4fff496930549ac61",
    },
    "v3.17.3": {
        struct(os = "darwin", arch = "amd64"): "20ef8df4671349a6fc556a621be1170dd709c6c0cf5f7e83a2d9fb0515fd97fc",
        struct(os = "darwin", arch = "arm64"): "89aec43ce07b06239f1bba4a6507236bb48ae487bc5065a8e254d3ce58a16997",
        struct(os = "linux", arch = "amd64"): "ee88b3c851ae6466a3de507f7be73fe94d54cbf2987cbaa3d1a3832ea331f2cd",
        struct(os = "linux", arch = "arm64"): "7944e3defd386c76fd92d9e6fec5c2d65a323f6fadc19bfb5e704e3eee10348e",
        struct(os = "windows", arch = "amd64"): "8ea93e2f6285e649dede583ac90ff8cdb938ca53ec6cf5fe909f2303fbc22d96",
        struct(os = "windows", arch = "arm64"): "70ce9dfdbc1ce6142626a829dbdc5920405146f3ce4dc6f6e6739dd308cc7baf",
    },
    "v3.13.1": {
        struct(os = "darwin", arch = "amd64"): "e207e009b931162b0383b463c333a2792355200e91dbcf167c97c150e9f5fedb",
        struct(os = "darwin", arch = "arm64"): "46596d6d2d9aa545eb74f40684858fac0841df373ca760af1259d3493161d8c9",
        struct(os = "linux", arch = "amd64"): "98c363564d00afd0cc3088e8f830f2a0eeb5f28755b3d8c48df89866374a1ed0",
        struct(os = "linux", arch = "arm64"): "8c4a0777218b266a7b977394aaf0e9cef30ed2df6e742d683e523d75508d6efe",
        struct(os = "windows", arch = "amd64"): "6e16fbc5e50a5841be2dc725e790234f09aa2a5ebe289493c90f65ecae7b156f",
        # NB: There is no Windows build available for the ARM64 architecture.
    },
}

_DEFAULT_TOOL_VERSION = "v4.1.1"

def known_release_versions():
    return _TOOLS_BY_RELEASE.keys()

HelmInfo = provider(
    doc = "Details pertaining to the Helm toolchain.",
    fields = {
        "tool": "Helm tool to invoke",
        "version": "This tool's released version name",
    },
)

HelmToolInfo = provider(
    doc = "Details pertaining to the Helm tool.",
    fields = {
        "binary": "Helm tool to invoke",
        "version": "This tool's released version name",
    },
)

def _helm_tool_impl(ctx):
    return [HelmToolInfo(
        binary = ctx.executable.binary,
        version = ctx.attr.version,
    )]

helm_tool = rule(
    implementation = _helm_tool_impl,
    attrs = {
        "binary": attr.label(
            mandatory = True,
            allow_single_file = True,
            executable = True,
            cfg = "exec",
            doc = "Helm tool to invoke",
        ),
        "version": attr.string(
            mandatory = True,
            doc = "This tool's released version name",
        ),
    },
)

def _toolchain_impl(ctx):
    tool = ctx.attr.tool[HelmToolInfo]
    toolchain_info = platform_common.ToolchainInfo(
        helminfo = HelmInfo(
            tool = tool.binary,
            version = tool.version,
        ),
    )
    return [toolchain_info]

helm_toolchain = rule(
    implementation = _toolchain_impl,
    attrs = {
        "tool": attr.label(
            mandatory = True,
            providers = [HelmToolInfo],
            cfg = "exec",
            doc = "Helm tool to use for inflating Helm charts.",
        ),
    },
)

# buildifier: disable=unnamed-macro
def declare_helm_toolchains(helm_tool):
    for version, platforms in _TOOLS_BY_RELEASE.items():
        for platform in platforms.keys():
            helm_toolchain(
                name = "{}_{}_{}".format(platform.os, platform.arch, version),
                tool = helm_tool,
            )

def _translate_host_platform(ctx):
    # NB: This is adapted from rules_go's "_detect_host_platform" function.
    os = ctx.os.name
    if os == "mac os x":
        os = "darwin"
    elif os.startswith("windows"):
        os = "windows"

    arch = ctx.os.arch
    if arch == "aarch64":
        arch = "arm64"
    elif arch == "x86_64":
        arch = "amd64"

    return os, arch

_MODULE_REPOSITORY_NAME = "rules_kustomize"
_CONTAINING_PACKAGE_PREFIX = "//kustomize/private/tools/helm"

def _download_tool_impl(ctx):
    if not ctx.attr.arch and not ctx.attr.os:
        os, arch = _translate_host_platform(ctx)
    else:
        if not ctx.attr.arch:
            fail('"os" is set but "arch" is not')
        if not ctx.attr.os:
            fail('"arch" is set but "os" is not')
        os, arch = ctx.attr.os, ctx.attr.arch
    version = ctx.attr.version

    sha256sum = _TOOLS_BY_RELEASE[version][struct(os = os, arch = arch)]
    if not sha256sum:
        fail('No Helm tool is available for OS "{}" and CPU architecture "{}" at version {}'.format(os, arch, version))
    ctx.report_progress('Downloading Helm tool for OS "{}" and CPU architecture "{}" at version {}.'.format(os, arch, version))
    ctx.download_and_extract(
        url = "https://get.helm.sh/helm-{}-{}-{}.{}".format(
            version,
            os,
            arch,
            "zip" if os == "windows" else "tar.gz",
        ),
        stripPrefix = "{}-{}".format(os, arch),
        sha256 = sha256sum,
    )

    ctx.template(
        "BUILD.bazel",
        Label("{}:BUILD.tool.bazel".format(_CONTAINING_PACKAGE_PREFIX)),
        executable = False,
        substitutions = {
            "{containing_package_prefix}": "@{}{}".format(_MODULE_REPOSITORY_NAME, _CONTAINING_PACKAGE_PREFIX),
            "{extension}": ".exe" if os == "windows" else "",
            "{version}": version,
        },
    )
    return None

_download_tool = repository_rule(
    implementation = _download_tool_impl,
    attrs = {
        "arch": attr.string(),
        "os": attr.string(),
        "version": attr.string(
            values = _TOOLS_BY_RELEASE.keys(),
            default = _DEFAULT_TOOL_VERSION,
        ),
    },
)

# buildifier: disable=unnamed-macro
def declare_bazel_toolchains(version, toolchain_prefix):
    native.constraint_value(
        name = version,
        constraint_setting = "{}:tool_version".format(_CONTAINING_PACKAGE_PREFIX),
    )
    constraint_value_prefix = "@{}//kustomize/private/tools".format(_MODULE_REPOSITORY_NAME)
    for platform in _TOOLS_BY_RELEASE[version].keys():
        native.toolchain(
            name = "{}_{}_{}_toolchain".format(platform.os, platform.arch, version),
            exec_compatible_with = [
                "{}:cpu_{}".format(constraint_value_prefix, platform.arch),
                "{}:os_{}".format(constraint_value_prefix, platform.os),
            ],
            toolchain = toolchain_prefix + (":{}_{}_{}".format(platform.os, platform.arch, version)),
            toolchain_type = "@{}//tools/helm:toolchain_type".format(_MODULE_REPOSITORY_NAME),
        )

def _toolchains_impl(ctx):
    ctx.template(
        "BUILD.bazel",
        Label("{}:BUILD.toolchains.bazel".format(_CONTAINING_PACKAGE_PREFIX)),
        executable = False,
        substitutions = {
            "{containing_package_prefix}": "@{}{}".format(_MODULE_REPOSITORY_NAME, _CONTAINING_PACKAGE_PREFIX),
            "{tool_repo}": ctx.attr.tool_repo,
            "{version}": ctx.attr.version,
        },
    )

_toolchains_repo = repository_rule(
    implementation = _toolchains_impl,
    attrs = {
        "tool_repo": attr.string(mandatory = True),
        "version": attr.string(
            values = _TOOLS_BY_RELEASE.keys(),
            default = _DEFAULT_TOOL_VERSION,
        ),
    },
)

def download_tool(name, version = None):
    _download_tool(
        name = name,
        version = version,
    )
    _toolchains_repo(
        name = name + "_toolchains",
        tool_repo = name,
        version = version,
    )
